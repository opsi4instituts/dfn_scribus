; Copyright (c) uib gmbh (www.uib.de)
; This sourcecode is owned by uib gmbh
; and published under the Terms of the General Public License.
; credits: http://www.opsi.org/en/credits/

[Actions]
; common Values for (un)installation
include_insert "common.opsiinc"

if not(HasMinimumSpace ("%SystemDrive%", $MinimumSpace$))
	LogError "Not enough space on %SystemDrive%, " + $MinimumSpace$ + " on drive %SystemDrive% needed for " + $ProductId$
	isFatalError
	; Stop process and set installation status to failed
else
	
	comment "show product picture"
	ShowBitmap "%ScriptPath%\" + $ProductId$ + ".png" $ProductName$ 	
	
	if FileExists("%ScriptPath%\delsub64.opsiscript")
		comment "Start uninstall sub section"
		Sub "%ScriptPath%\delsub64.opsiscript"
	endif
	
	Message "Installing %installingProdName% (%installingProdVersion%) 64 Bit..."
	comment "Start setup program"
	Winbatch_install
	Sub_check_exitcode
		
	comment "Test for installation success"
	
	Set $SearchPattern$ = $ProductName$
	; Parameter: $SearchPattern$ Suchbegriff in Registry
	Sub_search_registry64_uninstall_keys
	; Rückgabewert: $ResultList$ gefundene Einträge

	if ( count ($ResultList$) = "0" )
		logError "Fatal: After Installation " + $ProductName$ + " could not be found in " + $RegPathUninstall$
		isFatalError
	else
		comment "Successful Installation"

		Set $RegId$ = takeSTring(0, $ResultList$)			
		Set $PathToExe$ = GetRegistryStringValue64("[" + $RegPathUninstall$ + "\" + $RegId$ + "] DisplayIcon")

		comment "delete the automatically generated desktopicon (named with version)"
		Linkfolder_uninstall_desktopicon

		set $DesktopIcon$ = getProductProperty("desktopicon","no")
		if $DesktopIcon$ = "yes"
			comment "Create common desktop icon"
			Linkfolder_install_desktopicon 
		endif

		comment "include custom post install file"
		set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
		if not ($CustomPostInstall$ = "none")
			if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
				include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
			endif
		endif
	endif
endif

[Winbatch_install]
; === Nullsoft Scriptable Install System ================================================================
"%ScriptPath%\$SetupFile$" /S

[Linkfolder_install_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""

set_link
 	name: $ProductName$
 	target: $PathToExe$
end_link

[Linkfolder_uninstall_desktopicon]
set_basefolder common_desktopdirectory
set_subfolder ""
delete_element $RegId$

